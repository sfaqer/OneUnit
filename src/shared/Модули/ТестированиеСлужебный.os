#Использовать collectionos

Функция РазвернутьСписокТестовыхНаборов(Знач КаталогиТестов, Знач ФайлыТестов, Знач ИскатьВПодкаталогах) Экспорт

	Результат = Новый СписокМассив;

	Для каждого Каталог Из КаталогиТестов Цикл

		Для каждого Тест Из НайтиФайлы(Каталог, "*.os", ИскатьВПодкаталогах) Цикл
			Результат.Добавить(Тест);
		КонецЦикла;

	КонецЦикла;

	Для каждого Тест Из ФайлыТестов Цикл
		Результат.Добавить(Новый Файл(Тест));
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция КраткоеОписаниеОшибки(ИнформацияОбОшибке) Экспорт

	// Уберём артефакты которые приезжают из asserts
	ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "[Failed] ", "");

	ПозицияСтрокиСтекТрейса = СтрНайти(ОписаниеОшибки, "Стек трейс:");
	Если ПозицияСтрокиСтекТрейса > 0 Тогда
		ОписаниеОшибки = Лев(ОписаниеОшибки, ПозицияСтрокиСтекТрейса - 2);
		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, Символы.ПС, " ");
	КонецЕсли;

	Возврат ОписаниеОшибки;

КонецФункции

Функция ПодробноеОписаниеОшибки(Префикс, ИнформацияОбОшибке) Экспорт

	ОписаниеОшибки = СтрШаблон(
		"%1 (%2:%3)",
		КраткоеОписаниеОшибки(ИнформацияОбОшибке),
		ИнформацияОбОшибке.ИмяМодуля,
		ИнформацияОбОшибке.НомерСтроки
	);

	Если ЗначениеЗаполнено(ИнформацияОбОшибке.Параметры) Тогда
		ОписаниеОшибки = ОписаниеОшибки + Символы.ПС + "Параметры: " + ИнформацияОбОшибке.Параметры;
	КонецЕсли;

	Результат = СтрШаблон(
		"
		|%1:
		|%2
		|%3
		|",
		Префикс,
		ОписаниеОшибки,
		СтекВызовов(ИнформацияОбОшибке)
	);

	Если ЗначениеЗаполнено(ИнформацияОбОшибке.Причина) Тогда
		Результат = Результат + ПодробноеОписаниеОшибки("По причине", ИнформацияОбОшибке.Причина);
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция СтекВызовов(ИнформацияОбОшибке)

	Стек = ИнформацияОбОшибке.ПолучитьСтекВызовов();

	Если Не ЗначениеЗаполнено(Стек) Тогда
		Возврат "";
	КонецЕсли;

	Возврат ПроцессорыКоллекций.ИзКоллекции(Стек)
		.Первые(Стек.Количество() - 1) // Последний фрейм это служебный вызов обещания
		.Обработать("(Кадр) ->
			|	СтрШаблон(""  -> %1 (%3:%2)"", Кадр.Метод, Кадр.НомерСтроки, Кадр.ИмяМодуля)")
		.ВСтроку(Символы.ПС);

КонецФункции
