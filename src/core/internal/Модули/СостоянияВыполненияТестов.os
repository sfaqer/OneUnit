#Использовать collectionos

Перем Успех Экспорт;
Перем Пропущен Экспорт;
Перем Прерван Экспорт;
Перем Ошибка Экспорт;
Перем Сломан Экспорт;

Перем _КодировкиUTF;
Перем ПриоритетыСостояний;
Перем СостоянияПриоритетов;

Функция СимволСостояния(Состояние) Экспорт

	Если _КодировкиUTF.Содержит(Консоль.КодировкаВходногоПотока) Тогда
		СимволУспех   = "✔";
		СимволПропуск = "↷";
		СимволПрерван = "⊚";
		СимволОшибка  = "✘";
		СимволСломан  = "⊝";
	Иначе
		СимволУспех   = "V";
		СимволПропуск = "О";
		СимволПрерван = "0";
		СимволОшибка  = "X";
		СимволСломан  = "-";
	КонецЕсли;

	Если Состояние = Успех Тогда
		Возврат СимволУспех;
	ИначеЕсли Состояние = Пропущен Тогда
		Возврат СимволПропуск;
	ИначеЕсли Состояние = Прерван Тогда
		Возврат СимволПрерван;
	ИначеЕсли Состояние = Ошибка Тогда
		Возврат СимволОшибка;
	Иначе
		Возврат СимволСломан;
	КонецЕсли;

КонецФункции

Функция УровеньЛогаСостояния(Состояние) Экспорт

	Если Состояние = Ошибка Или Состояние = Сломан Тогда
		Уровень = УровниЛога.Ошибка;
	ИначеЕсли Состояние = Пропущен Или Состояние = Прерван Тогда
		Уровень = УровниЛога.Предупреждение;
	Иначе
		Уровень = УровниЛога.Информация;
	КонецЕсли;

	Возврат Уровень;

КонецФункции

Функция НаихудшееСостояние(Первое, Второе) Экспорт
	Возврат СостояниеПриоритета(Макс(ПриоритетСостояния(Первое), ПриоритетСостояния(Второе)));
КонецФункции

Функция ПриоритетСостояния(Состояние)
	Возврат ПриоритетыСостояний.Получить(Состояние).Получить();
КонецФункции

Функция СостояниеПриоритета(Приоритет)
	Возврат СостоянияПриоритетов.Получить(Приоритет).Получить();
КонецФункции

_КодировкиUTF = Списки.ИзЭлементов(
	КодировкаТекста.UTF8,
	КодировкаТекста.UTF8NoBOM,
	КодировкаТекста.UTF16
);

Успех = "Успех";
Пропущен = "Пропущен";
Прерван = "Прерван";
Ошибка = "Ошибка";
Сломан = "Сломан";

ПриоритетыСостояний = Новый КартаСоответствие();
ПриоритетыСостояний.Вставить(Успех, 0);
ПриоритетыСостояний.Вставить(Пропущен, 1);
ПриоритетыСостояний.Вставить(Прерван, 2);
ПриоритетыСостояний.Вставить(Ошибка, 3);
ПриоритетыСостояний.Вставить(Сломан, 4);

СостоянияПриоритетов = Новый КартаСоответствие();
СостоянияПриоритетов.Вставить(0, Успех);
СостоянияПриоритетов.Вставить(1, Пропущен);
СостоянияПриоритетов.Вставить(2, Прерван);
СостоянияПриоритетов.Вставить(3, Ошибка);
СостоянияПриоритетов.Вставить(4, Сломан);
