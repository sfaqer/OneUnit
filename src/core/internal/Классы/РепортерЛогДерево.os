#Использовать collectionos

Перем _Лог;
Перем _РепортерСтатистика;

Перем Префикс;
Перем ТекущийКонтейнер;

Перем ЗапаркованныйКонтейнер;

Процедура ВывестиТестПлан(ТестПлан) Экспорт

	ИсполнениеТестПланНачало(ТестПлан);

	Для каждого Набор Из ТестПлан.Дети() Цикл

		ИсполнениеТестНаборНачало(Набор);

		Для каждого Тест Из Набор.Дети() Цикл
			ВывестиТест(Тест);
		КонецЦикла;

		ИсполнениеТестНаборКонец(Набор, Неопределено);

	КонецЦикла;

КонецПроцедуры

Процедура ВывестиТест(Тест)

	ИсполнениеТестНачало(Тест);

	Для каждого ВложенныйТест Из Тест.Дети() Цикл
		ВывестиТест(ВложенныйТест);
	КонецЦикла;

	ИсполнениеТестКонец(Тест, Неопределено);

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестПланНачало")
Процедура ИсполнениеТестПланНачало(ТестПлан) Экспорт

	Префикс          = Новый СписокМассив;
	ТекущийКонтейнер = ТестПлан;

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестПланКонец")
Процедура ИсполнениеТестПланКонец(ТестПлан) Экспорт

	Префикс          = Неопределено;
	ТекущийКонтейнер = Неопределено;

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестНаборНачало")
Процедура ИсполнениеТестНаборНачало(ТестНабор) Экспорт
	ЗапаркованныйКонтейнер = ТестНабор;
КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестНаборКонец")
Процедура ИсполнениеТестНаборКонец(ТестНабор, Результат) Экспорт

	РаспарковатьКонтейнер(Результат);
	Префикс.Удалить(Префикс.Получить(Префикс.ВГраница()));
	ТекущийКонтейнер = ТестНабор.Родитель();

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестНачало")
Процедура ИсполнениеТестНачало(Тест) Экспорт
	РаспарковатьКонтейнер();
	Если Тест.ТипОпределения() = ТипыОпределенийТестов.Контейнер Тогда
		ВыводКонтейнера(Тест);
	КонецЕсли;
КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестКонец")
Процедура ИсполнениеТестКонец(Тест, Результат) Экспорт

	Если Тест.ТипОпределения() = ТипыОпределенийТестов.Контейнер Тогда

		Префикс.Удалить(Префикс.Получить(Префикс.ВГраница()));
		ТекущийКонтейнер = Тест.Родитель();

	Иначе

		ЭтоПоследний = ТекущийКонтейнер.Дети().Индекс(Тест) = ТекущийКонтейнер.Дети().ВГраница();

		Если ЭтоПоследний Тогда
			Префикс.Добавить("└─");
		Иначе
			Префикс.Добавить("├─");
		КонецЕсли;

		ТекущийКонтейнер = Тест;

		ВывестиВЛог(Результат);

		Префикс.Удалить(Префикс.Получить(Префикс.ВГраница()));

		ТекущийКонтейнер = Тест.Родитель();

	КонецЕсли;

КонецПроцедуры

Процедура ВыводКонтейнера(Контейнер, РезультатВыполнения = Неопределено) Экспорт

	ЭтоПоследний = ТекущийКонтейнер.Дети().Индекс(Контейнер) = ТекущийКонтейнер.Дети().ВГраница();

	Если ЭтоПоследний Тогда
		Префикс.Добавить("└─");
	Иначе
		Префикс.Добавить("├─");
	КонецЕсли;

	ТекущийКонтейнер = Контейнер;

	ВывестиВЛог(РезультатВыполнения);

	Если ЭтоПоследний Тогда
		Префикс.Установить(Префикс.ВГраница(), "    ");
	Иначе
		Префикс.Установить(Префикс.ВГраница(), "│   ");
	КонецЕсли;

КонецПроцедуры

Процедура ВывестиВЛог(РезультатВыполнения)

	Поля = Новый КартаСоответствие();
	Поля.Вставить("УровеньДерева", СтрСоединить(Префикс.ВМассив(), ""));
	Поля.Вставить("ИмяТеста",      ТекущийКонтейнер);

	Если РезультатВыполнения <> Неопределено Тогда

		Уровень = СостоянияВыполненияТестов.УровеньЛогаСостояния(РезультатВыполнения.Состояние());

		Причины = РезультатВыполнения.ОписанияПричин();
		Если Не Причины.Пусто() Тогда
			Поля.Вставить("Причина", Причины);
		КонецЕсли;

		Статистика = _РепортерСтатистика.СтатистикаТеста(ТекущийКонтейнер);

		Поля.Вставить("Результат", СостоянияВыполненияТестов.СимволСостояния(РезультатВыполнения.Состояние()));
		Поля.Вставить(
			"ВремяВыполнения",
			 Статистика.ПолучитьИлиУмолчание("ВремяВыполнения", 0)
		);

	Иначе
		Уровень = УровниЛога.Информация;
	КонецЕсли;

	_Лог.ПоляИз(Поля.КлючиИЗначения())
		.Вывести("", Уровень);

КонецПроцедуры

Процедура РаспарковатьКонтейнер(РезультатВыполнения = Неопределено)
	Если ЗначениеЗаполнено(ЗапаркованныйКонтейнер) Тогда
		ВыводКонтейнера(ЗапаркованныйКонтейнер, РезультатВыполнения);
		ЗапаркованныйКонтейнер = Неопределено;
	КонецЕсли;
КонецПроцедуры

&Желудь
Процедура ПриСозданииОбъекта(
	&Лог("oscript.lib.oneunit.core") Лог,
	&Пластилин РепортерСтатистика)
	_Лог = Лог;
	_РепортерСтатистика = РепортерСтатистика;
КонецПроцедуры
