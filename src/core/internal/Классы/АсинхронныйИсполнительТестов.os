#Использовать async
#Использовать collectionos

Перем _Поделка;
Перем _СтандартныйПотокВывода;
Перем _СтандартныйПотокОшибок;
Перем _ПубликаторСобытий;

Процедура Исполнить(ТестПлан) Экспорт

    _ПубликаторСобытий.ОпубликоватьСобытие(ТестПлан, "ИсполнениеТестПланНачало", Новый Массив);

	Наборы = ТестПлан.Дети();

    Для Каждого Набор Из Наборы Цикл

        _ПубликаторСобытий.ОпубликоватьСобытие(Набор, "ИсполнениеТестНаборНачало", Новый Массив);

        РезультатПропустить = ВыполнитьМетод(
            ЭтотОбъект,
            "ПропуститьОпределение",
            Новый Массив(Массивы.ИзЭлементов(Набор)),
            0
        );

        Если Пропустить(Набор, РезультатПропустить) Тогда
            Продолжить;
        КонецЕсли;

        Если Не ПроверитьСозданиеНабора(Набор) Тогда
            Продолжить;
        КонецЕсли;

        РезультатВыполненияНабора = Новый Структура(
            "Состояние, Причины, Вывод",
            СостоянияВыполненияТестов.Успех,
            Новый СписокМассив,
            ""
        );

        ОбъединитьРезультатыВыполнения(
            РезультатВыполненияНабора,
            ОбработатьСобытияНабора(Набор, Набор.ПередВсеми())
        );

        Если РезультатВыполненияНабора.Состояние = СостоянияВыполненияТестов.Успех Тогда

            Для Каждого Тест Из Набор.Дети() Цикл
                ВыполнитьТест(Тест, Набор, РезультатВыполненияНабора);
            КонецЦикла;

        КонецЕсли;

        ОбъединитьРезультатыВыполнения(
            РезультатВыполненияНабора,
            ОбработатьСобытияНабора(Набор, Набор.ПослеВсех())
        );

        Если РезультатВыполненияНабора.Состояние = СостоянияВыполненияТестов.Успех
            И Набор.Дети().Пусто() Тогда
            РезультатВыполненияНабора.Состояние = СостоянияВыполненияТестов.Пропущен;
            РезультатВыполненияНабора.Причины.Добавить("В наборе не найдены тесты");
        КонецЕсли;

        РезультатВыполненияНабора = _Поделка.НайтиЖелудь(
            "РезультатВыполненияТеста",
            Массивы.ИзЭлементов(
                РезультатВыполненияНабора.Состояние,
                РезультатВыполненияНабора.Причины,
                РезультатВыполненияНабора.Вывод
            )
        );

        _ПубликаторСобытий.ОпубликоватьСобытие(
            Набор,
            "ИсполнениеТестНаборКонец",
            Массивы.ИзЭлементов(РезультатВыполненияНабора)
        );

    КонецЦикла;

    _ПубликаторСобытий.ОпубликоватьСобытие(ТестПлан, "ИсполнениеТестПланКонец", Новый Массив);

КонецПроцедуры

Функция ОбработатьСобытияНабора(Набор, События)

    Результат = Новый Структура(
        "Состояние, Причины, Вывод",
        СостоянияВыполненияТестов.Успех,
        Новый СписокМассив,
        ""
    );

    ТестНабор = Набор.ТестНабор();

    Для Каждого ИмяМетода Из События Цикл

        ОбъединитьРезультатыВыполнения(
            Результат,
            ВыполнитьМетод(
                ТестНабор,
                ИмяМетода,
                Новый Массив,
                Набор.Таймаут()
            )
        );

    КонецЦикла;

    Возврат Результат;

КонецФункции

Процедура ВыполнитьТест(Тест, ОпределениеТестНабора, РезультатВыполненияНабора)

    _ПубликаторСобытий.ОпубликоватьСобытие(Тест, "ИсполнениеТестНачало", Новый Массив);

    РезультатПропустить = ВыполнитьМетод(
        ЭтотОбъект,
        "ПропуститьОпределение",
        Новый Массив(Массивы.ИзЭлементов(Тест)),
        0
    );

    Если Пропустить(Тест, РезультатПропустить) Тогда
        Возврат;
    КонецЕсли;

    Если Тест.ТипОпределения() = ТипыОпределенийТестов.Контейнер Тогда

        Для Каждого ВложенныйТест Из Тест.Дети() Цикл
            ВыполнитьТест(ВложенныйТест, ОпределениеТестНабора, РезультатВыполненияНабора);
        КонецЦикла;

        // Контейнер всегда зелёный
        Результат = _Поделка.НайтиЖелудь(
            "РезультатВыполненияТеста",
            Массивы.ИзЭлементов(
                СостоянияВыполненияТестов.Успех,
                Списки.ИзЭлементов(),
                ""
            )
        );

        _ПубликаторСобытий.ОпубликоватьСобытие(Тест, "ИсполнениеТестКонец", Массивы.ИзЭлементов(Результат));

        Возврат;

    КонецЕсли;

    РезультатВыполнения = Новый Структура(
        "Состояние, Причины, Вывод",
        СостоянияВыполненияТестов.Успех,
        Новый СписокМассив,
        ""
    );

    ТестНабор = ОпределениеТестНабора.ТестНабор();

    Для Каждого ИмяМетода Из ОпределениеТестНабора.ПередКаждым() Цикл

        ОбъединитьРезультатыВыполнения(
            РезультатВыполнения,
            ВыполнитьМетод(
                ТестНабор,
                ИмяМетода,
                Новый Массив,
                ОпределениеТестНабора.Таймаут()
            )
        );

    КонецЦикла;

    Если РезультатВыполнения.Состояние = СостоянияВыполненияТестов.Успех Тогда

        ОбъединитьРезультатыВыполнения(
            РезультатВыполнения,
            ВыполнитьМетод(
                ТестНабор,
                Тест.ИмяМетода(),
                Тест.Параметры().ВМассив(),
                Тест.Таймаут()
            )
        );

    КонецЕсли;

    Для Каждого ИмяМетода Из ОпределениеТестНабора.ПослеКаждого() Цикл

        ОбъединитьРезультатыВыполнения(
            РезультатВыполнения,
            ВыполнитьМетод(
                ТестНабор,
                ИмяМетода,
                Новый Массив,
                ОпределениеТестНабора.Таймаут()
            )
        );

    КонецЦикла;

    Результат = _Поделка.НайтиЖелудь(
        "РезультатВыполненияТеста",
        Массивы.ИзЭлементов(
            РезультатВыполнения.Состояние,
            РезультатВыполнения.Причины,
            РезультатВыполнения.Вывод
        )
    );

    _ПубликаторСобытий.ОпубликоватьСобытие(Тест, "ИсполнениеТестКонец", Массивы.ИзЭлементов(Результат));

КонецПроцедуры

Функция ВыполнитьМетод(Объект, ИмяМетода, Параметры, Таймаут)

    Делегат   = Новый Действие(Объект, ИмяМетода);

    Состояние = СостоянияВыполненияТестов.Успех;
    Причина   = NULL;

    ПотокВывода = Новый ПотокВПамяти();
    Консоль.УстановитьПотокВывода(ПотокВывода);
    Консоль.УстановитьПотокОшибок(ПотокВывода);

    Попытка
        РезультатВыполненияМетода = Обещания.ВыполнитьДелегат(Делегат, Параметры, Истина)
            .Получить(Таймаут);
    Исключение

        Причина   = ИнформацияОбОшибке();

        // Опустим первый стек, это служебный re-throw от async
        Если ЗначениеЗаполнено(Причина.Причина) Тогда
            Причина = Причина.Причина;
        КонецЕсли;

        Если ТипЗнч(Причина.Параметры) = Тип("ИсключениеОшибкаУтверждения") Тогда
            Состояние = СостоянияВыполненияТестов.Ошибка;
        ИначеЕсли ТипЗнч(Причина.Параметры) = Тип("ИсключениеТестПрерван") Тогда
            Состояние = СостоянияВыполненияТестов.Прерван;
        Иначе
            Состояние = СостоянияВыполненияТестов.Сломан;
        КонецЕсли;

    КонецПопытки;

    Консоль.УстановитьПотокВывода(_СтандартныйПотокВывода);
    Консоль.УстановитьПотокОшибок(_СтандартныйПотокОшибок);

    Возврат Новый Структура(
        "Состояние, Причины, Вывод, РезультатВыполненияМетода",
        Состояние,
        Списки.ИзЭлементов(Причина),
        ПрочитатьВывод(ПотокВывода),
        РезультатВыполненияМетода
    );

КонецФункции

Функция ПрочитатьВывод(Поток)

    Поток.СброситьБуферы();
    Поток.Перейти(0);

    ЧтениеТекста = Новый ЧтениеТекста();
    ЧтениеТекста.Открыть(Поток, Консоль.КодировкаВходногоПотока);
    Результат = ЧтениеТекста.Прочитать();

    ЧтениеТекста.Закрыть();
    Поток.Закрыть();

    Возврат Результат;

КонецФункции

Функция Пропустить(Определение, РезультатПропустить)

    Если ТипЗнч(Определение) = Тип("ОпределениеТеста") Тогда
        ИмяСобытия = "ИсполнениеТестКонец";
    Иначе
        ИмяСобытия = "ИсполнениеТестНаборКонец";
    КонецЕсли;

    Если РезультатПропустить.Состояние = СостоянияВыполненияТестов.Ошибка
        Или РезультатПропустить.Состояние = СостоянияВыполненияТестов.Сломан
        Или РезультатПропустить.Состояние = СостоянияВыполненияТестов.Прерван Тогда

        Пропустить = Истина;

        Результат = _Поделка.НайтиЖелудь(
            "РезультатВыполненияТеста",
            Массивы.ИзЭлементов(
                РезультатПропустить.Состояние,
                РезультатПропустить.Причины,
                РезультатПропустить.Вывод
            )
        );

    ИначеЕсли РезультатПропустить.РезультатВыполненияМетода.Пропустить Тогда

        Пропустить = Истина;

        Результат = _Поделка.НайтиЖелудь(
            "РезультатВыполненияТеста",
            Массивы.ИзЭлементов(
                СостоянияВыполненияТестов.Пропущен,
                Списки.ИзЭлементов(РезультатПропустить.РезультатВыполненияМетода.Причина),
                ""
            )
        );

    Иначе

        Пропустить = Ложь;

    КонецЕсли;

    Если Пропустить Тогда

        _ПубликаторСобытий.ОпубликоватьСобытие(
            Определение,
            ИмяСобытия,
            Массивы.ИзЭлементов(Результат)
        );

    КонецЕсли;

    Возврат Пропустить;

КонецФункции

Функция ПропуститьОпределение(Определение) Экспорт

    ОпределениеНабора = Определение;

    Пока ТипЗнч(ОпределениеНабора) <> Тип("ОпределениеТестНабора") Цикл
        ОпределениеНабора = ОпределениеНабора.Родитель();
    КонецЦикла;

    Результат = Новый Структура("Пропустить, Причина", Ложь, "");

    Причина = Определение.Условия()
        .ПроцессорКоллекции()
        .Обработать("Условие -> Условие.Выполнять(ТестНабор)",
            Новый Структура("ТестНабор", ОпределениеНабора.ТестНабор()))
        .Фильтровать("Выполнять -> Выполнять.Результат = Ложь")
        .Обработать("Выполнять -> Выполнять.Причина")
        .ПолучитьПервый();

    Если ЗначениеЗаполнено(Причина) Тогда

        Результат.Пропустить = Истина;
        Результат.Причина    = Причина;

    КонецЕсли;

    Возврат Результат;

КонецФункции

Процедура ОбъединитьРезультатыВыполнения(Первый, Второй)

    Первый.Состояние = СостоянияВыполненияТестов.НаихудшееСостояние(
        Первый.Состояние,
        Второй.Состояние
    );

    Первый.Причины.ДобавитьВсе(Второй.Причины);
    Первый.Вывод = Первый.Вывод + Второй.Вывод;

КонецПроцедуры

Функция ПроверитьСозданиеНабора(Набор) Экспорт

    Результат = ВыполнитьМетод(Набор, "ТестНабор", Новый Массив(Массивы.ИзЭлементов()), 0);

    Если Результат.Состояние <> СостоянияВыполненияТестов.Успех Тогда

        Результат = _Поделка.НайтиЖелудь(
            "РезультатВыполненияТеста",
            Массивы.ИзЭлементов(
                Результат.Состояние,
                Результат.Причины,
                Результат.Вывод
            )
        );

        _ПубликаторСобытий.ОпубликоватьСобытие(
            Набор,
            "ИсполнениеТестНаборКонец",
            Массивы.ИзЭлементов(Результат)
        );

        Возврат Ложь;

    ИначеЕсли Результат.РезультатВыполненияМетода = Неопределено Тогда

        Попытка
            ВызватьИсключение "Не удаётся инстанцировать тестовый набор";
        Исключение
            ИнформацияОбОшибке = ИнформацияОбОшибке();
        КонецПопытки;

        Результат = _Поделка.НайтиЖелудь(
            "РезультатВыполненияТеста",
            Массивы.ИзЭлементов(
                СостоянияВыполненияТестов.Сломан,
                Списки.ИзЭлементов(ИнформацияОбОшибке),
                Результат.Вывод
            )
        );

        _ПубликаторСобытий.ОпубликоватьСобытие(
            Набор,
            "ИсполнениеТестНаборКонец",
            Массивы.ИзЭлементов(Результат)
        );

        Возврат Ложь;

    КонецЕсли;

    Возврат Истина;

КонецФункции

&Желудь
&Прозвище("ИсполнительТестов")
Процедура ПриСозданииОбъекта(
    &Пластилин Поделка,
    &Пластилин ПубликаторСобытий)

    _Поделка                = Поделка;
    _ПубликаторСобытий      = ПубликаторСобытий;
    _СтандартныйПотокВывода = Консоль.ОткрытьСтандартныйПотокВывода();
    _СтандартныйПотокОшибок = Консоль.ОткрытьСтандартныйПотокОшибок();

КонецПроцедуры
