#Использовать fluent
#Использовать "../../../shared"

#Область ОписаниеПеременных

Перем _Лог;
Перем _РежимВыводаЛога;

Перем СтатистикаТестПлана;
Перем СтатистикаТестНаборов;
Перем СтатистикаТестов;

Перем Результаты;

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция СтатистикаТестПлана() Экспорт
	Возврат СтатистикаТестПлана;
КонецФункции

Функция СтатистикаНабора(Знач ТестНабор) Экспорт

	Пока ТипЗнч(ТестНабор) <> Тип("ОпределениеТестНабора") Цикл
		ТестНабор = ТестНабор.Родитель();
	КонецЦикла;

	Возврат СтатистикаТестНаборов
		.ВычислитьЕслиОтсутствует(ТестНабор, "К -> Новый КартаСоответствие")
		.Получить();

КонецФункции

Функция СтатистикаТеста(Тест) Экспорт

	Возврат СтатистикаТестов
		.ВычислитьЕслиОтсутствует(Тест, "К -> Новый КартаСоответствие")
		.Получить();

КонецФункции

Функция Результат(Определение) Экспорт
	Возврат Результаты.Получить(Определение)
		.Получить();
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

&ПодпискаНаСобытие("ИсполнениеТестПланНачало")
Процедура ИсполнениеТестПланНачало(ТестПлан) Экспорт

	СтатистикаТестПлана   = Новый КартаСоответствие();
	СтатистикаТестНаборов = Новый КартаСоответствие();
	СтатистикаТестов      = Новый КартаСоответствие();

	Результаты = Новый КартаСоответствие;

	СтатистикаТестПлана.Вставить("ВремяНачала", ТекущаяУниверсальнаяДатаВМиллисекундах());
	СтатистикаТестПлана.Вставить("ДатаНачала", МестноеВремя('00010101' + (ТекущаяУниверсальнаяДатаВМиллисекундах() / 1000)));

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестПланКонец")
Процедура ИсполнениеТестПланКонец(ТестПлан) Экспорт

	СтатистикаТестПлана.Вставить("ВремяЗавершения", ТекущаяУниверсальнаяДатаВМиллисекундах());
	СтатистикаТестПлана.Вставить("ДатаЗавершения", МестноеВремя('00010101' + (ТекущаяУниверсальнаяДатаВМиллисекундах() / 1000)));

	СтатистикаТестПлана.Вставить(
		"ВремяВыполнения",
		СтатистикаТестПлана.ПолучитьИлиУмолчание("ВремяЗавершения", 0)
			- СтатистикаТестПлана.ПолучитьИлиУмолчание("ВремяНачала", 0)
	);

	ВывестиОшибки();
	ВывестиСтатистику();

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестНаборНачало")
Процедура ИсполнениеТестНаборНачало(ТестНабор) Экспорт
	СтатистикаНабора(ТестНабор).Вставить("ВремяНачала", ТекущаяУниверсальнаяДатаВМиллисекундах());
	СтатистикаНабора(ТестНабор).Вставить("ДатаНачала", МестноеВремя('00010101' + (ТекущаяУниверсальнаяДатаВМиллисекундах() / 1000)));
	СтатистикаТестПлана.Слить("НаборовОбнаружено", 1, "Было, Добавлено -> Было + Добавлено");
КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестНаборКонец")
Процедура ИсполнениеТестНаборКонец(ТестНабор, Результат) Экспорт

	СтатистикаНабора(ТестНабор).Вставить("ВремяЗавершения", ТекущаяУниверсальнаяДатаВМиллисекундах());
	СтатистикаНабора(ТестНабор).Вставить("ДатаЗавершения", МестноеВремя('00010101' + (ТекущаяУниверсальнаяДатаВМиллисекундах() / 1000)));

	СтатистикаНабора(ТестНабор).Вставить(
		"ВремяВыполнения",
		СтатистикаНабора(ТестНабор).ПолучитьИлиУмолчание("ВремяЗавершения", 0)
			- СтатистикаНабора(ТестНабор).ПолучитьИлиУмолчание("ВремяНачала", 0)
	);

	Результаты.Вставить(ТестНабор, Результат);

	// Проверим есть ли упавшие дети
	Если СтатистикаНабора(ТестНабор).ПолучитьИлиУмолчание("ТестовСломано", 0) > 0 Тогда
		Состояние = СостоянияВыполненияТестов.Сломан;
	ИначеЕсли СтатистикаНабора(ТестНабор).ПолучитьИлиУмолчание("ТестовОшибка", 0) > 0 Тогда
		Состояние = СостоянияВыполненияТестов.Ошибка;
	Иначе
		Состояние = СостоянияВыполненияТестов.Успех;
	КонецЕсли;

	Состояние = СостоянияВыполненияТестов.НаихудшееСостояние(Состояние, Результат.Состояние());

	Если Состояние = СостоянияВыполненияТестов.Пропущен Тогда
		Ключ = "НаборовПропущено";
	ИначеЕсли Состояние = СостоянияВыполненияТестов.Прерван Тогда
		Ключ = "НаборовПрервано";
	ИначеЕсли Состояние = СостоянияВыполненияТестов.Ошибка Тогда
		Ключ = "НаборовОшибка";
	ИначеЕсли Состояние = СостоянияВыполненияТестов.Сломан Тогда
		Ключ = "НаборовСломано";
	Иначе
		Ключ = "НаборовУспешно";
	КонецЕсли;

	СтатистикаТестПлана.Слить(Ключ, 1, "Было, Добавлено -> Было + Добавлено");

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестНачало")
Процедура ИсполнениеТестНачало(Тест) Экспорт

	Время = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Дата  = МестноеВремя('00010101' + (ТекущаяУниверсальнаяДатаВМиллисекундах() / 1000));

	СтатистикаТеста(Тест).Вставить("ВремяНачала", Время);
	СтатистикаТеста(Тест).Вставить("ДатаНачала", Дата);

	Если Тест.ТипОпределения() = ТипыОпределенийТестов.Тест Тогда
		СтатистикаТестПлана.Слить("ТестовОбнаружено", 1, "Было, Добавлено -> Было + Добавлено");
		СтатистикаНабора(Тест).Слить("ТестовОбнаружено", 1, "Было, Добавлено -> Было + Добавлено");
	КонецЕсли;

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестКонец")
Процедура ИсполнениеТестКонец(Тест, Результат) Экспорт

	СтатистикаТеста(Тест).Вставить(
		"ВремяЗавершения",
		ТекущаяУниверсальнаяДатаВМиллисекундах()
	);

	СтатистикаТеста(Тест).Вставить(
		"ДатаЗавершения",
		МестноеВремя('00010101' + (ТекущаяУниверсальнаяДатаВМиллисекундах() / 1000))
	);

	СтатистикаТеста(Тест).Вставить(
		"ВремяВыполнения",
		СтатистикаТеста(Тест).ПолучитьИлиУмолчание("ВремяЗавершения", 0)
			- СтатистикаТеста(Тест).ПолучитьИлиУмолчание("ВремяНачала", 0)
	);

	Результаты.Вставить(Тест, Результат);

	Если Тест.ТипОпределения() = ТипыОпределенийТестов.Тест Тогда

		Если Результат.Состояние() = СостоянияВыполненияТестов.Успех Тогда
			Ключ = "ТестовУспешно";
		ИначеЕсли Результат.Состояние() = СостоянияВыполненияТестов.Пропущен Тогда
			Ключ = "ТестовПропущено";
		ИначеЕсли Результат.Состояние() = СостоянияВыполненияТестов.Прерван Тогда
			Ключ = "ТестовПрервано";
		ИначеЕсли Результат.Состояние() = СостоянияВыполненияТестов.Ошибка Тогда
			Ключ = "ТестовОшибка";
		ИначеЕсли Результат.Состояние() = СостоянияВыполненияТестов.Сломан Тогда
			Ключ = "ТестовСломано";
		Иначе
			ВызватьИсключение "Неизвестное состояние выполнения теста";
		КонецЕсли;

		СтатистикаТестПлана.Слить(Ключ, 1, "Было, Добавлено -> Было + Добавлено");
		СтатистикаНабора(Тест).Слить(Ключ, 1, "Было, Добавлено -> Было + Добавлено");

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВывестиОшибки()

	Ошибки = Результаты.КлючиИЗначения()
		.ПроцессорКоллекции()
		.Фильтровать(
			"(КиЗ) -> Возврат КиЗ.Значение.Состояние() = СостоянияВыполненияТестов.Ошибка
			|	Или КиЗ.Значение.Состояние() = СостоянияВыполненияТестов.Сломан"
		)
		.ВМассив();

	Для Каждого ТестИРезультат Из Ошибки Цикл

		Тест                = ТестИРезультат.Ключ;
		РезультатВыполнения = ТестИРезультат.Значение;

		Сообщение = Новый Массив;

		Сообщение.Добавить(СтрШаблон(
			"
			|%1
			|──────────────────────────────────────────────────────────────────────────────────────────────────────────
			|",
			Тест.ПолноеИмя()
		));

		РезультатВыполнения.Причины()
			.ПроцессорКоллекции()
			.Обработать("Причина -> ТестированиеСлужебный.ПодробноеОписаниеОшибки(""Причина"", Причина)")
			.ДляКаждого("Причина -> Сообщение.Добавить(Причина)", Новый Структура("Сообщение", Сообщение));

		Вывод = РезультатВыполнения.Вывод();

		Если Не ПустаяСтрока(Вывод) Тогда

			Сообщение.Добавить(СтрШаблон(
				"
				|Вывод:
				|%1
				|",
				Вывод
			));

		КонецЕсли;

		_Лог.КритичнаяОшибка(СтрСоединить(Сообщение));

	КонецЦикла;

КонецПроцедуры

Процедура ВывестиСтатистику()

	ЕстьОшибки = Результаты.Значения()
		.ПроцессорКоллекции()
		.ЛюбойСоответствует(
			"(З) -> З.Состояние() = СостоянияВыполненияТестов.Ошибка Или З.Состояние() = СостоянияВыполненияТестов.Сломан"
		);

	Если Не ЕстьОшибки И _РежимВыводаЛога = РежимыВыводаЛога.Ничего Тогда
		Возврат;
	КонецЕсли;

	ВремяНачала = СтатистикаТестПлана.ПолучитьИлиУмолчание("ВремяНачала", 0);
	ВремяЗавершения = СтатистикаТестПлана.ПолучитьИлиУмолчание("ВремяЗавершения", 0);

	НаборовОбнаружено = СтатистикаТестПлана.ПолучитьИлиУмолчание("НаборовОбнаружено", 0);
	НаборовПропущено = СтатистикаТестПлана.ПолучитьИлиУмолчание("НаборовПропущено", 0);
	НаборовПрервано = СтатистикаТестПлана.ПолучитьИлиУмолчание("НаборовПрервано", 0);
	НаборовУспешно = СтатистикаТестПлана.ПолучитьИлиУмолчание("НаборовУспешно", 0);
	НаборовОшибка = СтатистикаТестПлана.ПолучитьИлиУмолчание("НаборовОшибка", 0)
		+ СтатистикаТестПлана.ПолучитьИлиУмолчание("НаборовСломано", 0);

	ТестовОбнаружено = СтатистикаТестПлана.ПолучитьИлиУмолчание("ТестовОбнаружено", 0);
	ТестовПропущено = СтатистикаТестПлана.ПолучитьИлиУмолчание("ТестовПропущено", 0);
	ТестовПрервано = СтатистикаТестПлана.ПолучитьИлиУмолчание("ТестовПрервано", 0);
	ТестовУспешно = СтатистикаТестПлана.ПолучитьИлиУмолчание("ТестовУспешно", 0);
	ТестовОшибка = СтатистикаТестПлана.ПолучитьИлиУмолчание("ТестовОшибка", 0)
		+ СтатистикаТестПлана.ПолучитьИлиУмолчание("ТестовСломано", 0);

	МаксимумРазрядов = Макс(
		НаборовОбнаружено, НаборовПропущено, НаборовПрервано, НаборовУспешно, НаборовОшибка,
		ТестовОбнаружено, ТестовПропущено, ТестовПрервано, ТестовУспешно, ТестовОшибка
	);

	МаксимумРазрядов = СтрДлина(МаксимумРазрядов);

	СообщениеСтатистикаНаборов = СтрШаблон(
		"[         %1 Наборов обнаружено         ]
		|[         %2 Наборов пропущено          ]
		|[         %3 Наборов прервано           ]
		|[         %4 Наборов успешных           ]
		|[         %5 Наборов ошибочных          ]",
		ПривестиКДлине(НаборовОбнаружено, МаксимумРазрядов),
		ПривестиКДлине(НаборовПропущено, МаксимумРазрядов),
		ПривестиКДлине(НаборовПрервано, МаксимумРазрядов),
		ПривестиКДлине(НаборовУспешно, МаксимумРазрядов),
		ПривестиКДлине(НаборовОшибка, МаксимумРазрядов)
	);

	СообщениеСтатистикаТестов = СтрШаблон(
		"[         %1 Тестов обнаружено          ]
		|[         %2 Тестов пропущено           ]
		|[         %3 Тестов прервано            ]
		|[         %4 Тестов успешных            ]
		|[         %5 Тестов ошибочных           ]",
		ПривестиКДлине(ТестовОбнаружено, МаксимумРазрядов),
		ПривестиКДлине(ТестовПропущено, МаксимумРазрядов),
		ПривестиКДлине(ТестовПрервано, МаксимумРазрядов),
		ПривестиКДлине(ТестовУспешно, МаксимумРазрядов),
		ПривестиКДлине(ТестовОшибка, МаксимумРазрядов)
	);

	_Лог.Информация(
		"
		|  Запуск тестов завершился за %1 мс
		|
		|%2
		|
		|%3
		|",
		ВремяЗавершения - ВремяНачала,
		СообщениеСтатистикаНаборов,
		СообщениеСтатистикаТестов
	);

КонецПроцедуры

Функция ПривестиКДлине(Знач Значение, Длина)

	Результат = Строка(Значение);

	Пока СтрДлина(Результат) < Длина Цикл
		Результат = " " + Результат;
	КонецЦикла;

	Возврат Результат;

КонецФункции

#КонецОбласти

&Желудь
Процедура ПриСозданииОбъекта(
	&Лог("oscript.lib.oneunit.core") Лог,
	&Деталька("OneUnit.РежимВыводаЛога") РежимВыводаЛога)

	_Лог = Лог;

	_РежимВыводаЛога = РежимВыводаЛога;

КонецПроцедуры
