#Использовать collectionos

Перем _Лог;
Перем _РепортерСтатистика;
Перем _КодировкиUTF;

Процедура ВывестиТестПлан(ТестПлан) Экспорт

	ТестПлан.Дети()
		.ПроцессорКоллекции()
		.Развернуть("Набор -> Набор.Дети().ПроцессорКоллекции()")
		.ДляКаждого("Тест -> ВывестиТест(Тест)", ЭтотОбъект);

КонецПроцедуры

Процедура ВывестиТест(Тест) Экспорт

	Тест.Дети()
		.ДляКаждого("Тест -> ВывестиТест(Тест)", ЭтотОбъект);

	ИсполнениеТестКонец(Тест, Неопределено);

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестКонец")
Процедура ИсполнениеТестКонец(Тест, Результат) Экспорт

	Если Тест.ТипОпределения() = ТипыОпределенийТестов.Тест Тогда
		ВывестиВЛог(Тест, Результат);
	КонецЕсли;

КонецПроцедуры

Процедура ВывестиВЛог(Определение, РезультатВыполнения)

	Поля = Новый КартаСоответствие;
	Поля.Вставить("ИмяТеста",  Определение.ПолноеИмя());

	Если РезультатВыполнения = Неопределено Тогда
		Уровень = УровниЛога.Информация;
	Иначе

		Уровень = СостоянияВыполненияТестов.УровеньЛогаСостояния(РезультатВыполнения.Состояние());

		Причины = РезультатВыполнения.ОписанияПричин();
		Если Не Причины.Пусто() Тогда
			Поля.Вставить("Причина", Причины);
		КонецЕсли;

		Статистика = _РепортерСтатистика.СтатистикаТеста(Определение);

		Поля.Вставить("Результат", СостоянияВыполненияТестов.СимволСостояния(РезультатВыполнения.Состояние()));
		Поля.Вставить(
			"ВремяВыполнения",
				Статистика.ПолучитьИлиУмолчание("ВремяЗавершения", 0) -
				Статистика.ПолучитьИлиУмолчание("ВремяНачала", 0)
		);

	КонецЕсли;

	_Лог.ПоляИз(Поля.КлючиИЗначения())
		.Вывести("", Уровень);

КонецПроцедуры

&Желудь
Процедура ПриСозданииОбъекта(
	&Лог("oscript.lib.oneunit.core") Лог,
	&Пластилин РепортерСтатистика)

	_Лог = Лог;

	_РепортерСтатистика = РепортерСтатистика;

	_КодировкиUTF = Списки.ИзЭлементов(
		КодировкаТекста.UTF8,
		КодировкаТекста.UTF8NoBOM,
		КодировкаТекста.UTF16
	);

КонецПроцедуры
