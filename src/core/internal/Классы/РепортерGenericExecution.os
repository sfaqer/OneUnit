Перем _ПутьКОтчету;

Перем ЗаписьXML;
Перем _РепортерСтатистика;

&ПодпискаНаСобытие("ИсполнениеТестПланНачало")
Процедура ИсполнениеТестПланНачало(ТестПлан) Экспорт

	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(_ПутьКОтчету);

	ЗаписьXML.ЗаписатьНачалоЭлемента("testExecutions");
	ЗаписьXML.ЗаписатьАтрибут("version", 1);

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестПланКонец")
Процедура ИсполнениеТестПланКонец(ТестПлан) Экспорт
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.Закрыть();
КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестНаборНачало")
Процедура ИсполнениеТестНаборНачало(ТестНабор) Экспорт
	ЗаписьXML.ЗаписатьНачалоЭлемента("file");
	ЗаписьXML.ЗаписатьАтрибут("path", ТестНабор.Путь());
КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестНаборКонец")
Процедура ИсполнениеТестНаборКонец(ТестНабор, Результат) Экспорт
	ЗаписьXML.ЗаписатьКонецЭлемента();
КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестКонец")
Процедура ИсполнениеТестКонец(Тест, Результат) Экспорт

	Если Тест.ТипОпределения() = ТипыОпределенийТестов.Контейнер Тогда
		Возврат;
	КонецЕсли;

	ЗаписьXML.ЗаписатьНачалоЭлемента("testCase");
	ЗаписьXML.ЗаписатьАтрибут("name", Тест.ПолноеИмя());
	ЗаписьXML.ЗаписатьАтрибут(
		"duration",
		_РепортерСтатистика.СтатистикаТеста(Тест).ПолучитьИлиУмолчание("ВремяВыполнения", 0)
	);

	Если Результат.Состояние() = СостоянияВыполненияТестов.Пропущен
		Или Результат.Состояние() = СостоянияВыполненияТестов.Прерван Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("skipped");
		ЗаписьXML.ЗаписатьКонецЭлемента();
	ИначеЕсли Результат.Состояние() = СостоянияВыполненияТестов.Ошибка Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("failure");
		ЗаписьXML.ЗаписатьКонецЭлемента();
	ИначеЕсли Результат.Состояние() = СостоянияВыполненияТестов.Сломан Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("error");
		ЗаписьXML.ЗаписатьКонецЭлемента();
	Иначе
		// no-op
	КонецЕсли;

	ЗаписьXML.ЗаписатьКонецЭлемента();

КонецПроцедуры

&Желудь
Процедура ПриСозданииОбъекта(&Блестяшка ПутьКОтчетуGenericExecution, &Пластилин РепортерСтатистика)
	_ПутьКОтчету        = ПутьКОтчетуGenericExecution;
	_РепортерСтатистика = РепортерСтатистика;
КонецПроцедуры
