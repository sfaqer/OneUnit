#Использовать collectionos
#Использовать "../../../shared"

Перем _ПутьКОтчету;

Перем _РепортерСтатистика;

&ПодпискаНаСобытие("ИсполнениеТестПланКонец")
Процедура ИсполнениеТестПланКонец(ТестПлан) Экспорт

	Если Не ЗначениеЗаполнено(_ПутьКОтчету) Тогда
		Возврат;
	КонецЕсли;

	ВывестиТестПлан(ТестПлан);

КонецПроцедуры

Процедура ВывестиТестПлан(ТестПлан)

	СтатистикаТестПлана =  _РепортерСтатистика.СтатистикаТестПлана();

	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(_ПутьКОтчету, "UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();

	ЗаписьXML.ЗаписатьНачалоЭлемента("testsuites");

	ЗаписьXML.ЗаписатьАтрибут("name",       "Test run");

	ЗаписьXML.ЗаписатьАтрибут("tests",   СтатистикаТестПлана.ПолучитьИлиУмолчание("ТестовОбнаружено", 0));
	ЗаписьXML.ЗаписатьАтрибут("skipped", СтатистикаТестПлана.ПолучитьИлиУмолчание("ТестовПропущено", 0)
		+ СтатистикаТестПлана.ПолучитьИлиУмолчание("ТестовПрервано", 0));
	ЗаписьXML.ЗаписатьАтрибут("failures", СтатистикаТестПлана.ПолучитьИлиУмолчание("ТестовОшибка", 0));
	ЗаписьXML.ЗаписатьАтрибут("errors", СтатистикаТестПлана.ПолучитьИлиУмолчание("ТестовСломано", 0));
	ЗаписьXML.ЗаписатьАтрибут("assertions", 0);

	ЗаписьXML.ЗаписатьАтрибут(
		"time",
		(СтатистикаТестПлана.ПолучитьИлиУмолчание("ВремяЗавершения", 0)
		- СтатистикаТестПлана.ПолучитьИлиУмолчание("ВремяНачала", 0))
		/ 1000
	);

	ЗаписьXML.ЗаписатьАтрибут(
		"timestamp",
		XMLСтрока(СтатистикаТестПлана.ПолучитьИлиУмолчание("ДатаЗавершения", '00010101'))
	);

	ЗаписьXML.ЗаписатьНачалоЭлемента("properties");

	СистемнаяИнформация = Новый СистемнаяИнформация();

	Рефлектор = Новый Рефлектор();
	Свойства = Рефлектор.ПолучитьТаблицуСвойств(СистемнаяИнформация);

	Для каждого Свойство Из Свойства Цикл

		Значение = СистемнаяИнформация[Свойство.Имя];

		Если ТипЗнч(Значение) = Тип("ФиксированныйМассив") Тогда
			Значение = СтрСоединить(Новый Массив(Значение), ",");
		КонецЕсли;

		ЗаписьXML.ЗаписатьНачалоЭлемента("property");
		ЗаписьXML.ЗаписатьАтрибут("name",  Свойство.Имя);
		ЗаписьXML.ЗаписатьАтрибут("value", Значение);

		ЗаписьXML.ЗаписатьКонецЭлемента();

	КонецЦикла;

	ЗаписьXML.ЗаписатьКонецЭлемента();

	Для каждого Набор Из ТестПлан.Дети() Цикл
		ВывестиТестНабор(ЗаписьXML, Набор);
	КонецЦикла;

	ЗаписьXML.ЗаписатьКонецЭлемента();

	ЗаписьXML.Закрыть();

КонецПроцедуры

Процедура ВывестиТестНабор(ЗаписьXML, ТестНабор) Экспорт

	Если ТипЗнч(ТестНабор) = Тип("ОпределениеТестНабора") Тогда
		Статистика = _РепортерСтатистика.СтатистикаНабора(ТестНабор);
	Иначе
		Статистика = _РепортерСтатистика.СтатистикаТеста(ТестНабор);
	КонецЕсли;

	ЗаписьXML.ЗаписатьНачалоЭлемента("testsuite");

	ЗаписьXML.ЗаписатьАтрибут("name", ТестНабор.Имя());
	ЗаписьXML.ЗаписатьАтрибут("tests", Статистика.ПолучитьИлиУмолчание("ТестовОбнаружено", 0));
	ЗаписьXML.ЗаписатьАтрибут("skipped", Статистика.ПолучитьИлиУмолчание("ТестовПропущено", 0)
		+ Статистика.ПолучитьИлиУмолчание("ТестовПрервано", 0));
	ЗаписьXML.ЗаписатьАтрибут("failures", Статистика.ПолучитьИлиУмолчание("ТестовОшибка", 0));
	ЗаписьXML.ЗаписатьАтрибут("errors", Статистика.ПолучитьИлиУмолчание("ТестовСломано", 0));
	ЗаписьXML.ЗаписатьАтрибут("assertions", 0);

	ЗаписьXML.ЗаписатьАтрибут("time",
		(Статистика.ПолучитьИлиУмолчание("ВремяЗавершения", 0)
		- Статистика.ПолучитьИлиУмолчание("ВремяНачала", 0))
		/ 1000
	);

	ЗаписьXML.ЗаписатьАтрибут(
		"timestamp",
		XMLСтрока(Статистика.ПолучитьИлиУмолчание("ДатаЗавершения", '00010101'))
	);

	ЗаписьXML.ЗаписатьАтрибут("file", ТестНабор.Путь());

	Результат = _РепортерСтатистика.Результат(ТестНабор);

	Вывод = Результат.Вывод();
	Причины = "";
	ПричинаПропуска = "";

	Если Результат.Состояние() = СостоянияВыполненияТестов.Ошибка
		Или Результат.Состояние() = СостоянияВыполненияТестов.Сломан Тогда

		Причины = Результат.Причины()
			.ПроцессорКоллекции()
			.Обработать("Причина -> ТестированиеСлужебный.ПодробноеОписаниеОшибки(""Причина"", Причина)")
			.ВСтроку(Символы.ПС);

	КонецЕсли;

	Если Результат.Состояние() = СостоянияВыполненияТестов.Пропущен
		Или Результат.Состояние() = СостоянияВыполненияТестов.Прерван Тогда
		ПричинаПропуска = СтрСоединить(Результат.Причины().ВМассив(), Символы.ПС);
	КонецЕсли;

	ЗаписьXML.ЗаписатьНачалоЭлемента("properties");

	ЗаписьXML.ЗаписатьНачалоЭлемента("property");
	ЗаписьXML.ЗаписатьАтрибут("name", "tags");
	ЗаписьXML.ЗаписатьАтрибут("value", СтрСоединить(ТестНабор.Теги().ВМассив(), ","));
	ЗаписьXML.ЗаписатьКонецЭлемента();

	Если Не ПустаяСтрока(ПричинаПропуска) Тогда

		ЗаписьXML.ЗаписатьНачалоЭлемента("property");
		ЗаписьXML.ЗаписатьАтрибут("name", "skippedReason");
		ЗаписьXML.ЗаписатьАтрибут("value", ПричинаПропуска);
		ЗаписьXML.ЗаписатьКонецЭлемента();

	КонецЕсли;

	ЗаписьXML.ЗаписатьКонецЭлемента();

	ЗаписьXML.ЗаписатьНачалоЭлемента("system-out");

	Если Не ПустаяСтрока(Вывод) Тогда
		ЗаписьXML.ЗаписатьТекст(Символы.ПС);
		ЗаписьXML.ЗаписатьСекциюCDATA(Вывод);
		ЗаписьXML.ЗаписатьТекст(Символы.ПС);
	КонецЕсли;

	ЗаписьXML.ЗаписатьКонецЭлемента();

	ЗаписьXML.ЗаписатьНачалоЭлемента("system-err");

	Если Не ПустаяСтрока(Причины) Тогда
		ЗаписьXML.ЗаписатьТекст(Символы.ПС);
		ЗаписьXML.ЗаписатьСекциюCDATA(Причины);
		ЗаписьXML.ЗаписатьТекст(Символы.ПС);
	КонецЕсли;

	ЗаписьXML.ЗаписатьКонецЭлемента();

	// TODO: Выводить ли детей если пропущен?
	Если Результат.Состояние() = СостоянияВыполненияТестов.Успех Тогда

		Для Каждого Тест Из ТестНабор.Дети() Цикл

			Если Тест.ТипОпределения() = ТипыОпределенийТестов.Контейнер Тогда
				ВывестиТестНабор(ЗаписьXML, Тест);
			Иначе
				ВывестиТест(ЗаписьXML, Тест);
			КонецЕсли;

		КонецЦикла;

	КонецЕсли;

	ЗаписьXML.ЗаписатьКонецЭлемента();

КонецПроцедуры

Процедура ВывестиТест(ЗаписьXML, Тест) Экспорт

	СтатистикаТеста  = _РепортерСтатистика.СтатистикаТеста(Тест);

	ЗаписьXML.ЗаписатьНачалоЭлемента("testcase");
	ЗаписьXML.ЗаписатьАтрибут("name", Тест.Имя());

	Родитель = Тест.Родитель();

	Пока ТипЗнч(Родитель) <> Тип("ОпределениеТестНабора") Цикл
		Родитель = Родитель.Родитель();
	КонецЦикла;

	ЗаписьXML.ЗаписатьАтрибут("classname", Родитель.Имя());
	ЗаписьXML.ЗаписатьАтрибут("assertions", 0);

	ЗаписьXML.ЗаписатьАтрибут("time",
		(СтатистикаТеста.ПолучитьИлиУмолчание("ВремяЗавершения", 0)
		- СтатистикаТеста.ПолучитьИлиУмолчание("ВремяНачала", 0))
		/ 1000
	);

	ЗаписьXML.ЗаписатьАтрибут("file", Тест.Путь());
	ЗаписьXML.ЗаписатьАтрибут("line", 0);

	Результат = _РепортерСтатистика.Результат(Тест);

	Вывод = Результат.Вывод();

	Если Результат.Состояние() = СостоянияВыполненияТестов.Ошибка
		Или Результат.Состояние() = СостоянияВыполненияТестов.Сломан Тогда

		Если Результат.Состояние() = СостоянияВыполненияТестов.Ошибка Тогда
			ЗаписьXML.ЗаписатьНачалоЭлемента("failure");
		Иначе
			ЗаписьXML.ЗаписатьНачалоЭлемента("error");
		КонецЕсли;

		ЗаписьXML.ЗаписатьАтрибут("message", Результат.ОписанияПричин());
		ЗаписьXML.ЗаписатьАтрибут("type", "");

		ЗаписьXML.ЗаписатьТекст(Символы.ПС);

		Причины = Результат.Причины()
			.ПроцессорКоллекции()
			.Обработать("Причина -> ТестированиеСлужебный.ПодробноеОписаниеОшибки(""Причина"", Причина)")
			.ВСтроку(Символы.ПС);

		ЗаписьXML.ЗаписатьСекциюCDATA(Причины);
		ЗаписьXML.ЗаписатьТекст(Символы.ПС);

		ЗаписьXML.ЗаписатьКонецЭлемента();

	ИначеЕсли Результат.Состояние() = СостоянияВыполненияТестов.Пропущен
		Или Результат.Состояние() = СостоянияВыполненияТестов.Прерван Тогда

		ЗаписьXML.ЗаписатьНачалоЭлемента("skipped");
		ЗаписьXML.ЗаписатьАтрибут("message", СтрСоединить(Результат.Причины().ВМассив(), Символы.ПС));
		ЗаписьXML.ЗаписатьКонецЭлемента();

	КонецЕсли;

	ЗаписьXML.ЗаписатьНачалоЭлемента("properties");

	ЗаписьXML.ЗаписатьНачалоЭлемента("property");
	ЗаписьXML.ЗаписатьАтрибут("name", "tags");
	ЗаписьXML.ЗаписатьАтрибут("value", СтрСоединить(Тест.Теги().ВМассив(), ","));
	ЗаписьXML.ЗаписатьКонецЭлемента();

	ЗаписьXML.ЗаписатьКонецЭлемента();

	ЗаписьXML.ЗаписатьНачалоЭлемента("system-out");

	Если Не ПустаяСтрока(Вывод) Тогда
		ЗаписьXML.ЗаписатьТекст(Символы.ПС);
		ЗаписьXML.ЗаписатьСекциюCDATA(Вывод);
		ЗаписьXML.ЗаписатьТекст(Символы.ПС);
	КонецЕсли;

	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьНачалоЭлемента("system-err");
	ЗаписьXML.ЗаписатьКонецЭлемента();

	ЗаписьXML.ЗаписатьКонецЭлемента();

КонецПроцедуры

&Желудь
Процедура ПриСозданииОбъекта(&Блестяшка ПутьКОтчетуJUnit, &Пластилин РепортерСтатистика)
	_ПутьКОтчету        = ПутьКОтчетуJUnit;
	_РепортерСтатистика = РепортерСтатистика;
КонецПроцедуры
